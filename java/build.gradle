apply plugin: 'java'

sourceCompatibility = 1.7
version = '1.0'

// Define the Google API versions to use, allowing the build
// server to override via environment variables.
if (!project.hasProperty('googleClientVersion')) {
    ext.googleClientVersion = '1.19.0'
}
if (!project.hasProperty('mapsengineClientVersion')) {
    ext.mapsengineClientVersion = 'v1-rev45-' + googleClientVersion
}

repositories {
    mavenCentral()
}

dependencies {
    // Google client libraries
    compile "com.google.http-client:google-http-client-gson:" + project.ext.googleClientVersion
    compile "com.google.oauth-client:google-oauth-client-servlet:" + project.ext.googleClientVersion
    compile "com.google.apis:google-api-services-mapsengine:" + project.ext.mapsengineClientVersion
    compile "com.google.oauth-client:google-oauth-client-jetty:" + project.ext.googleClientVersion
    compile "com.google.maps:mapsengine-api-java-wrapper:0.2"

    // Other libraries
    compile 'org.mortbay.jetty:jetty:6.1.+'
    compile 'net.sf.opencsv:opencsv:2.3'
}

if (!project.hasProperty('mainClass')) {
    project.ext.mainClass = ''
}
task execute(type:JavaExec) {
    main = project.ext.mainClass
    classpath = sourceSets.main.runtimeClasspath
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

